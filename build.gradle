buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.2.0"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'stos.projects'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly('org.projectlombok:lombok')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.flywaydb:flyway-core')
    runtime('org.postgresql:postgresql')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

flyway {
    url = "jdbc:postgresql://${psqlDbHost}:${psqlDbPort}/${dbName}"
    user = psqlDbUser
    password = psqlDbPassword

    locations = ['filesystem:db/migration/sql']
}

/******************************************
 * Database tasks
 ******************************************/
task createScoreKeeperDb(type: Exec) {
    description 'Create the scorekeeper database on PostgreSQL.'
    group 'Database'
    executable 'createdb'
    doFirst {
        args "-U${psqlDbUser}", "${dbName}"
    }
}

task deleteScoreKeeperDb(type: Exec) {
    description 'Create the scorekeeper database on PostgreSQL.'
    group 'Database'
    executable 'psql'
    doFirst {
        args "-U${psqlDbUser}", "-c", "DROP DATABASE ${dbName}"
    }
}