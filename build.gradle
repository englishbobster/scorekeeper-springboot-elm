buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.2.0"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'stos.projects'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

def elmSource = "./src/Main.elm"
def jsMain = "${projectDir}/src/main/resources/static/js/Main.js"

repositories {
    mavenCentral()
}

dependencies {
    compileOnly('org.projectlombok:lombok')
    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('com.google.guava:guava:26.0-jre')
    compile('org.flywaydb:flyway-core')
    runtime('org.postgresql:postgresql')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('com.h2database:h2:1.4.197')
}

flyway {
    url = "jdbc:postgresql://${psqlDbHost}:${psqlDbPort}/${dbName}"
    user = psqlDbUser
    password = psqlDbPassword

    locations = ['filesystem:db/migration/sql']
}

/******************************************
 * Database tasks
 ******************************************/
task createScoreKeeperDb(type: Exec) {
    group 'Database'
    description 'Create the scorekeeper database on PostgreSQL.'
    executable 'createdb'
    doFirst {
        args "-U${psqlDbUser}", "${dbName}"
    }
}

task deleteScoreKeeperDb(type: Exec) {
    group 'Database'
    description 'Drop the scorekeeper database on PostgreSQL.'
    executable 'psql'
    doFirst {
        args "-U${psqlDbUser}", "-c", "DROP DATABASE ${dbName}"
    }
}

/***************************************
 * Front end tasks
 ***************************************/
task elmMake(type: Exec){
    group 'elm frontend'
    description 'Build the elm frontend'
    workingDir "${projectDir}/src/webapp/src"
    executable 'elm'
    doFirst {
        args "make", "${elmSource}", "--output=${jsMain}"
    }

}